buildscript {
    repositories {
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            name = "sponge"
            url = "https://repo.spongepowered.org/maven"
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

version = "1.0"
group = "superhelo.icrutils"
archivesBaseName = "icrutils"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "stable_39"

    def args = [
            "-Dmixin.hotSwap=true",
            "-Dmixin.debug.export=true",
            "-Dmixin.checks.interfaces=true",
            "-Dfml.coreMods.load=superhelo.icrutils.core.MixinEarlyInit"
    ]

    clientJvmArgs.addAll(args)
    serverJvmArgs.addAll(args)
    makeObfSourceJar = false
}

repositories {
    mavenCentral()
    maven {
        name 'blamejared'
        url 'https://maven.blamejared.com'
    }
    maven {
        name 'jei'
        url 'http://dvs1.progwml6.com/files/maven'
    }
    maven {
        name 'youyihj'
        url 'https://friendlyhj.github.io/maven'
    }
    maven {
        name 'cofh'
        url 'https://maven.covers1624.net'
    }
    maven {
        name 'curseforge'
        url 'https://www.cursemaven.com'
    }
    maven {
        name 'sponge'
        url 'https://repo.spongepowered.org/maven'
    }
    maven {
        name 'mixinbooter'
        url 'https://maven.cleanroommc.com'
    }
}

dependencies {
    deobfCompile "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.20.674"
    deobfCompile "curse.maven:AstralSorcery-241721:3044416"
    deobfCompile "curse.maven:ProjectE-226410:2702991"
    deobfCompile "mezz.jei:jei_1.12.2:4.16.1.302"
    deobfCompile "zone.rong:mixinbooter:4.2"

    compile "cofh:RedstoneFlux:1.12-2.1.0.+:universal"
    compile "curse.maven:Baubles-227083:2518667"
    compile "youyihj:zenutils:1.10.1"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

runClient {
    args '--username', 'dev_player'
}

jar {
    manifest.attributes.putAll([
            "FMLCorePlugin"              : "superhelo.icrutils.core.MixinEarlyInit",
            "FMLCorePluginContainsFMLMod": true,
            "ForceLoadAsMod"             : true
    ])
}

mixin {
    add sourceSets.main, "mixins.icrutils.refmap.json"
}
